buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

// don't lint for every build (run it manually when you want it to run)
tasks.whenTaskAdded { task ->
    if (task.name.equals('lint')) {
        task.enabled = false
    }
}


android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    defaultConfig {
        applicationId 'me.vickychijwani.spectre'
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1_003_000      // 3 digits each for major, minor, patch
        versionName '1.3.0'

        // enable support for vector drawables on API < 21
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    signingConfigs {
//        release {
//            storeFile(file(System.getenv('KEYSTORE') ?: '') ?: '')
//            storePassword(System.getenv('KEYSTORE_PASSWORD') ?: '')
//            keyAlias(System.getenv('KEY_ALIAS') ?: '')
//            keyPassword(System.getenv('KEY_PASSWORD') ?: '')
//        }
//    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
//            signingConfig signingConfigs.release
            minifyEnabled false   // FIXME disabled because login, revoke token strangely stopped working
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

// Fabric / Crashlytics
afterEvaluate {
    initFabricPropertiesIfNeeded()
}

def initFabricPropertiesIfNeeded() {
    def propertiesFile = file('crashlytics.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = 'This file is auto-generated from system environment variables.'
        ant.propertyfile(file: propertiesFile.path, comment: commentMessage) {
            entry(key: 'apiKey', value: crashlyticsApiKey)
            entry(key: 'apiSecret', value: crashlyticsApiSecret)
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // reactive extensions (rx)
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.trello:rxlifecycle:0.1.0'
    compile 'com.trello:rxlifecycle-components:0.1.0'

    // networking / communication / data storage
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup:otto:1.3.6'

    // UI
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:percent:24.0.0'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.0.Final'
    compile 'com.tsengvn:typekit:1.0.1'

    // misc
    compile 'com.jakewharton:butterknife:7.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.6@aar') {
        transitive = true
    }
    compile('com.crashlytics.sdk.android:answers:1.3.7@aar') {
        transitive = true
    }
    compile 'com.github.slugify:slugify:2.1.3'

    // debug
    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.3.1'
    debugCompile 'com.uphyca:stetho_realm:0.9.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
    debugCompile 'io.palaima.debugdrawer:debugdrawer:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-commons:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-okhttp:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-picasso:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-scalpel:0.7.0'
}
